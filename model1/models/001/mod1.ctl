$PROBLEM Model prepared for Finch-Studio test

$INPUT C NUM ID TIME AMT CMT EVID DV BLQ LLOQ 
       AGE SEX ALB SCR WT HT GENO BSA EGFR DOSE

$DATA ../../data/nmdat1.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN2 TRANS2

$PK
 
;log transformed PK params
TVKA = THETA(1)
KA   = TVKA*EXP(ETA(1))

TVV  = THETA(2)
V    = TVV*EXP(ETA(2))

TVCL = THETA(3)
CL   = TVCL*EXP(ETA(3))

S2 = V/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED*(1+EPS(1))

$THETA
(0, 1.5)   ;  1 KA (1/hr)
(0, 60)    ;  2 V  (L)
(0, 3.5)   ;  3 CL (L/hr)

$OMEGA
0.2   ; ETA(KA)
0.2   ; ETA(V)
0.2   ; ETA(CL)

$SIGMA
0.1     ; 1 prop error

$EST MAXEVAL=9999 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1 RANMETHOD=P MSFO=./mod1.msf 
$COV PRINT=E RANMETHOD=P
$TABLE NUM ID TIME DV MDV EVID IPRED NPDE CWRES NOPRINT ONEHEADER FILE=sdtab1
$TABLE NUM ID CL V  KA ETAS(1:LAST) NOAPPEND NOPRINT ONEHEADER FILE=patab1 ; model parameters
$TABLE NUM ID SEX GENO NOAPPEND ONEHEADER NOPRINT FILE=catab1 ; categorical covariates
$TABLE NUM ID AGE ALB SCR WT HT BSA EGFR DOSE NOAPPEND ONEHEADER NOPRINT FILE=cotab1 ; continuous covariates
